plugins {
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '5.+'
    id 'org.spongepowered.mixin' version '0.7-SNAPSHOT'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

version = '1.19.4-5.0.1'
group = 'com.andrews.table_top_craft'
archivesBaseName = 'table_top_craft'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: 'parchment', version: '1.19.3-2023.03.12-1.19.4'
  
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            mods {
                table_top_craft {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            mods {
                table_top_craft {
                    source sourceSets.main
                }
            }
        }
    }
}

mixin {
    add sourceSets.main, "table_top_craft.refmap.json"
    config "table_top_craft.mixins.json"
}

// Include resources generated by data generators.
//sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenCentral()
    maven { url "https://www.cursemaven.com" }
}

configurations {
    shade
    implementation.extendsFrom shade
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.19.4-45.1.0'
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    minecraftLibrary 'net.objecthunter:exp4j:0.4.8'
    shade 'net.objecthunter:exp4j:0.4.8'

    compileOnly fg.deobf("curse.maven:oculus-581495:4451142")
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "table_top_craft",
                "Specification-Vendor"    : "andrew0030",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "andrew0030",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

shadowJar {
    archiveClassifier = ''
    configurations = [project.configurations.shade]
    relocate 'net.objecthunter', "andrews.table_top_craft.shadowed.objecthunter"
    finalizedBy 'reobfShadowJar'
}

assemble.dependsOn shadowJar
reobf {
    shadowJar {}
}
jar.finalizedBy('reobfJar')

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}